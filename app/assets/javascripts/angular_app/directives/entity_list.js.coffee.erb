angular.module('InescApp').directive 'entityList', ['$filter', 'routing', ($filter, routing) ->
  currencyFilter = $filter('currency')
  buildList = (element, entities, year) ->
    currencyColumnClass = 'currency-column'
    columns = [
      { sTitle: 'Órgão', bVisible: false },
      { sTitle: 'Orçamento Autorizado do Órgão', bVisible: false },
      { sTitle: 'Unidade Orçamentária' }
      { sTitle: 'Orçamento Autorizado', sClass: currencyColumnClass }
    ]
    unidadesOrcamentarias = entities.filter((entity) -> entity.orgao?)
      .sort((a, b) ->
        if b.orgao == a.orgao
          b.amount - a.amount
        else
          b.orgao.amount - a.orgao.amount
      )
      .map (uo) ->
        orgao = uo.orgao
        orgaoUrl = routing.generateUrl(orgao, year)
        uoUrl = routing.generateUrl(uo, year)
        ["<a href='#{orgaoUrl}'>#{orgao.label}</a>",
         currencyFilter(orgao.amount, ''),
         "<a href='#{uoUrl}'>#{uo.label}</a>",
         currencyFilter(uo.amount, '')]

    element.dataTable
      aaData: unidadesOrcamentarias
      aoColumns: columns
      bPaginate: false
      bSort: false
      sDom: 'ft'
      oLanguage:
        sSearch: 'Filtrar por entidade: '
        sZeroRecords: 'Nenhum resultado encontrado.'
      fnDrawCallback: (oSettings) ->
        return if oSettings.aiDisplay.length == 0
        nTrs = element.children('tbody').children('tr')
        sLastGroup = ''

        for tr, i in nTrs
          iDisplayIndex = oSettings._iDisplayStart + i
          sGroup = oSettings.aoData[oSettings.aiDisplay[iDisplayIndex]]._aData[0]
          if sGroup != sLastGroup
            sGroupData = oSettings.aoData[oSettings.aiDisplay[iDisplayIndex]]._aData[1]
            nGroup = document.createElement('tr')
            nGroup.className = 'entity-group'
            nCell = document.createElement('td')
            nCell.innerHTML = sGroup
            nCellData = document.createElement('td')
            nCellData.innerHTML = sGroupData
            nCellData.className = currencyColumnClass
            nGroup.appendChild(nCell)
            nGroup.appendChild(nCellData)
            tr.parentNode.insertBefore(nGroup, tr)
            sLastGroup = sGroup

  restrict: 'E',
  template: '<table class="entity-table table table-bordered table-condensed"></table>',
  scope:
    entities: '='
  link: (scope, element, attributes) ->
    currentYear = new Date().getFullYear().toString()
    table = $(element).children('table')
    scope.$watch 'entities', ->
      entities = scope.entities
      buildList(table, entities, currentYear) if entities?
]

