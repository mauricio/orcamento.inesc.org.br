angular.module('InescApp').directive 'entityList', ['routing', (routing) ->
  processData = (entities, year) ->
    entities.filter((entity) -> entity.orgao?)
      .map (uo) ->
        orgao = uo.orgao
        orgaoUrl = routing.generateUrl(orgao, year)
        uoUrl = routing.generateUrl(uo, year)
        ["<a href='#{orgaoUrl}'>#{orgao.label}</a>",
         "<a href='#{uoUrl}'>#{uo.label}</a>"]

  buildList = (element, entities, year) ->
    columns = [
      { sTitle: 'Órgão', bVisible: false },
      { sTitle: '<span class="icon-sort-by-alphabet"></span><span class="icon-sort-by-alphabet-alt"></span> Unidades Orçamentárias (agrupadas por órgão)' }
    ]
    data = processData(entities, year)

    element.dataTable
      aoColumns: columns
      aaData: data
      bPaginate: false
      bSort: true
      sDom: 'ft'
      oLanguage:
        sSearch: 'Filtrar por entidade: <span class="icon-filter icon-large"></span>'
        sZeroRecords: 'Nenhum resultado encontrado.'
      fnDrawCallback: (oSettings) ->
        return if oSettings.aiDisplay.length == 0
        nTrs = element.children('tbody').children('tr')
        sLastGroup = ''

        for tr, i in nTrs
          iDisplayIndex = oSettings._iDisplayStart + i
          sGroup = oSettings.aoData[oSettings.aiDisplay[iDisplayIndex]]._aData[0]
          if sGroup != sLastGroup
            nGroup = document.createElement('tr')
            nGroup.className = 'entity-group'
            nCell = document.createElement('td')
            nCell.innerHTML = sGroup
            nGroup.appendChild(nCell)
            tr.parentNode.insertBefore(nGroup, tr)
            sLastGroup = sGroup

  restrict: 'E',
  template: '<table class="entity-table table table-bordered table-condensed"></table>',
  scope:
    entities: '='
  link: (scope, element, attributes) ->
    currentYear = new Date().getFullYear().toString()
    table = $(element).children('table')
    scope.$watch 'entities', ->
      entities = scope.entities
      buildList(table, entities, currentYear) if entities?
]

