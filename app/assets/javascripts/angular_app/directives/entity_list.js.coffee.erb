angular.module('InescApp').directive 'entityList', ['routing', (routing) ->
  buildList = (element, entities, year) ->
    columns = [
      { sTitle: 'Órgão', bVisible: false },
      { sTitle: '<span class="icon-sort-by-alphabet icon-large"></span><span class="icon-sort-by-alphabet-alt icon-large"></span> Unidade Orçamentária' }
    ]
    orgaos = entities.filter((entity) -> entity.orgao?)
      .sort((a, b) -> a.orgao.label > b.orgao.label)
      .map (uo) ->
        orgao = uo.orgao
        orgaoUrl = routing.generateUrl(orgao, year)
        uoUrl = routing.generateUrl(uo, year)
        ["<a href='#{orgaoUrl}'>#{orgao.label}</a>",
         "<a href='#{uoUrl}'>#{uo.label}</a>"]

    element.dataTable
      aaData: orgaos
      aoColumns: columns
      bPaginate: false
      sDom: 'ft'
      oLanguage:
        sSearch: 'Filtrar por entidade: '
        sZeroRecords: 'Nenhum resultado encontrado.'
      fnDrawCallback: (oSettings) ->
        return if oSettings.aiDisplay.length == 0
        nTrs = element.children('tbody').children('tr')
        iColspan = nTrs[0].getElementsByTagName('td').length
        sLastGroup = ''

        for tr, i in nTrs
          iDisplayIndex = oSettings._iDisplayStart + i
          sGroup = oSettings.aoData[oSettings.aiDisplay[iDisplayIndex]]._aData[0]
          if sGroup != sLastGroup
            nGroup = document.createElement('tr')
            nGroup.className = 'entity-group'
            nCell = document.createElement('td')
            nCell.colSpan = iColspan
            nCell.innerHTML = sGroup
            nGroup.appendChild(nCell)
            tr.parentNode.insertBefore(nGroup, tr)
            sLastGroup = sGroup

  restrict: 'E',
  template: '<table class="entity-table table table-bordered table-condensed"></table>',
  scope:
    entities: '='
  link: (scope, element, attributes) ->
    currentYear = new Date().getFullYear().toString()
    table = $(element).children('table')
    scope.$watch 'entities', ->
      entities = scope.entities
      buildList(table, entities, currentYear) if entities?
]

